// these are all Java projects
apply plugin: 'java'

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    markdownDoclet
    provided
}

dependencies {
    // slf4j
    compile group:'org.slf4j', name:'slf4j-api', version:'1.7.13'

    // better collections and other assorted utils
    compile group: 'com.google.guava', name:'guava', version:'19.0'

    // never write POJOs again
    compile 'org.projectlombok:lombok:1.16.6'

    // these are the EXACT dependencies one has to use when using the JUnit+Mockito combo to avoid Hamcrest
    // compilation/runtime problems
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'
    testCompile group: 'org.hamcrest', name: 'hamcrest-junit', version: '2.0.0.0'

    markdownDoclet "ch.raffael.pegdown-doclet:pegdown-doclet:1.1.1"

    provided "org.projectlombok:lombok:1.16.6"
}

javadoc.options {
    docletpath = configurations.markdownDoclet.files.asType(List) // gradle should relly make this simpler
    doclet = "ch.raffael.doclets.pegdown.PegdownDoclet"
    addStringOption("parse-timeout", "10")
}
// set all sources to UTF-8 for proper unicode support
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

// advanced delombok for all defined sourceSets, originally based on http://stackoverflow.com/q/24997441/44523
sourceSets.all {
    Task dependent = tasks.findByName("compile${name.capitalize()}Java")
    if (!dependent) dependent = compileJava

    // need to redefine name variable as otherwise Groovy gets a bit too excited and uses actual task name instead
    def sourceSetName = name
    def taskName = 'delombok' + name.capitalize()
    tasks.create(taskName) {
        description "Delomboks the ${name} source code tree"

        doLast {
            def sumTree = files( configurations.provided + compileClasspath )

            ant.taskdef(
                name: taskName,
                classname: 'lombok.delombok.ant.Tasks$Delombok',
                classpath: sumTree.asPath
            )
            ant."$taskName"(
                verbose: true,
                encoding: 'UTF-8',
                from: "src/${sourceSetName}/java",
                to: "build/src-delomboked/${sourceSetName}",
                classpath: sumTree.asPath
            )
        }
    }
    dependent.dependsOn taskName
}
